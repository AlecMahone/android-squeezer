buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')

    // Android support libraries
    // Note: these libraries require the "Google Repository" and "Android
    //       Support Repository" to be installed via the SDK manager.
    compile 'com.android.support:support-v4:21.0.2'
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.android.support:support-annotations:20.0.0'

    // Third party libraries
    compile 'com.google.guava:guava:18.0'
    // findbugs is required for Proguard to work with Guava.
    compile 'com.google.code.findbugs:jsr305:2.0.2'

    // EventBus, https://github.com/greenrobot/EventBus.
    compile 'de.greenrobot:eventbus:2.4.0'

    // Changelogs, see https://github.com/cketti/ckChangeLog.
    compile 'de.cketti.library.changelog:ckchangelog:1.2.0'
    compile 'com.crashlytics.android:crashlytics:1.+'
}

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.1"

    def gitHash = "git rev-parse --short HEAD".execute().text.trim()
    def hasModifiedDeletedOrOtherFiles = !"git ls-files -mdo --exclude-standard".execute().text.trim().isEmpty()
    def hasStagedFiles = !"git diff-index --no-ext-diff --name-only --cached HEAD".execute().text.trim().isEmpty()
    def dirtyWorkingCopy = hasModifiedDeletedOrOtherFiles || hasStagedFiles
    def gitDescription = dirtyWorkingCopy ? "${gitHash}-dirty" : gitHash

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19

        buildConfigField "String", "GIT_DESCRIPTION", "\"${gitDescription}\""
    }

    lintOptions {
        // Downgrade missing translations to non-fatal severity.
        warning 'MissingTranslation'
    }

    buildTypes {
        release {
            minifyEnabled true
            // You could use 'proguardFile "proguard.cfg"' here and get the
            // same effect, but this ensures that any changes to
            // proguard-android-optimize.txt are automatically included.
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile "proguard-crashlytics.cfg"
            proguardFile "proguard-eventbus.cfg"
            proguardFile "proguard-guava.cfg"
            proguardFile "proguard-squeezer.cfg"
        }
    }

    signingConfigs {
        if(project.hasProperty("Squeezer.properties")
                && file(project.property("Squeezer.properties")).exists()) {
            Properties props = new Properties()
            props.load(new FileInputStream(file(project.property("Squeezer.properties"))))
            release {
                storeFile file("keystore")
                storePassword props['key.store.password']
                keyAlias "squeezer"
                keyPassword props['key.alias.password']
            }
        } else {
            release {
                storeFile file("squeezer-local-release-key.keystore")
                storePassword "squeezer"
                keyAlias "squeezer"
                keyPassword "squeezer"
            }
        }
    }

    productFlavors {
        beta {
            versionCode 42
            versionName "1.2.7-beta-1"
            signingConfig android.signingConfigs.release
        }

        live {
            versionCode 41
            versionName "1.2.6"
            signingConfig android.signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        def apkSuffix = "-${variant.mergedFlavor.versionName}-${gitDescription}"
        variant.outputs.each { output ->
            def originalApkFile = output.outputFile
            output.outputFile = new File(
                    // Override this to change output directory
                    (String) originalApkFile.parent,
                    (String) originalApkFile.name.replace(".apk", "${apkSuffix}.apk")
            )
        }
        // Print the APK path after assemble is done
        variant.assemble.doLast {
            variant.outputs.each { output ->
                println output.outputFile
            }
        }

        // Print the APK path as the last step before install, at which point the apk is complete
        variant.install?.doFirst { // the install task can be null
            variant.outputs.each { output ->
                println output.outputFile
            }
        }
    }
}
